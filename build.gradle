plugins {
    id 'java-library'
    id 'maven-publish'
}

archivesBaseName = 'launchwrapper'

project.ext.asm_version = 9.7

dependencies {
    // implementation 'org.mcphackers.rdi:rdi:1.0'
    implementation name: 'rdi-1.0-SNAPSHOT'
    implementation "org.ow2.asm:asm:${project.asm_version}"
    implementation "org.ow2.asm:asm-tree:${project.asm_version}"
    implementation 'org.json:json:20230311'
    // I'll bring discord RPC support later, when I have an environment to compile natives

    testRuntimeOnly('org.junit.platform:junit-platform-launcher:1.5.2')
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.0.0'
}

test {
    useJUnitPlatform()
    testLogging {
        exceptionFormat = 'full'
        events = ["passed", "failed", "skipped"]
        showStandardStreams = true
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    group = 'org.mcphackers'
    version = '1.0-SNAPSHOT'

    repositories {
        flatDir {
            dirs "libs"
        }
        maven {
            url "https://libraries.minecraft.net/"
        }
        maven {
            url "https://maven.glass-launcher.net/releases"
        }
        maven {
            url "https://mcphackers.github.io/libraries/"
        }
        mavenCentral()
    }

    task sourcesJar(type: Jar) {
        archiveClassifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives jar
        archives sourcesJar
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.encoding = "UTF-8"

        // Not possible due to unsafe usage
        // if (JavaVersion.current().isJava9Compatible()) {
        // 	it.options.release.set(8)
        // }
    }
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    compileJava {
        // Minecraft is mostly compatible with java 5 up until the version we support, so why not?
        if (JavaVersion.current().ordinal() <= JavaVersion.VERSION_1_8.ordinal()) {
            sourceCompatibility = JavaVersion.VERSION_1_5
            targetCompatibility = JavaVersion.VERSION_1_5
        }
    }

    compileTestJava {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
				groupId = "${project.group}"
				artifactId = "${project.archivesBaseName}"
				version = "${project.version}"
            
                artifact jar
                artifact sourcesJar
            }
        }

        repositories {
            mavenLocal()
            
            def ENV = System.getenv()
            if (ENV.MAVEN_URL) {
                maven {
                    url ENV.MAVEN_URL
                    if (ENV.MAVEN_USERNAME) {
                        credentials {
                            username ENV.MAVEN_USERNAME
                            password ENV.MAVEN_PASSWORD
                        }
                    }
                }
            }
        }
    }
}